// Author: AntÃ³nio Vasconcelos
// Twitter: https://twitter.com/anthonws
// This query provides you the latest signature and platform (MoCamp) for Windows Defender AV
// --------------------------------------------------------------------------------------- //
// Define the time window
// Please note that results will vary depending on startDate
let startDate = ago(7d);
FileCreationEvents
| where InitiatingProcessCommandLine has "MpSigStub.exe"
//To exclude Engine Updates and non update events 
| where InitiatingProcessParentFileName != "AM_Engine.exe" and InitiatingProcessParentFileName != "wuauclt.exe"
// Comment the below line if you're looking specifically for a computer
| where EventTime > startDate
// Uncomment the line below when looking for info regarding a specific computer
//| and ComputerName == "COMPUTER"
| extend NewVersion=tostring(split(InitiatingProcessCommandLine, " ")[4]) 
| summarize arg_max(NewVersion, EventTime) by ComputerName
| project ComputerName , NewVersion
| join (FileCreationEvents
        | where FileName == "MsMpEng.exe"
        | where FolderPath has @"C:\ProgramData\Microsoft\Windows Defender\Platform\"
        | where EventTime > startDate
        | extend PlatformVersion=tostring(split(FolderPath, "\\", 5))
        | project ComputerName, PlatformVersion)
    on ComputerName
| project ComputerName , NewVersion , PlatformVersion
