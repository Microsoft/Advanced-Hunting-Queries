// Find all executions of a given Powersehll cmdlet (PowerSploit's "Invoke-ReflectivePEInjection in this case)
let powershellCommandName = "Invoke-ReflectivePEInjection";
MiscEvents | where ActionType == "PowerShellCommand" and AdditionalFields contains powershellCommandName
| project PowershellCommand=extractjson("$.Command", AdditionalFields, typeof(string)), InitiatingProcessCommandLine, InitiatingProcessParentFileName, EventTime, MachineId
| where PowershellCommand =~ powershellCommandName

// Investigate an alert or a machine?
// Using this query you can find which uncommon Powershell Cmdlets were executed in time vicinity to that alert (or to some other event).
let alertId = "636641078490537577_-1905871543";
// Query for machine and time period of the given alert
let alert = AlertEvents | where AlertId == alertId | summarize AlertFirstEventTime=min(EventTime) by MachineId;
let machineId = toscalar(alert | project MachineId);
let alertTime = toscalar(alert | project AlertFirstEventTime);
// Query for Powershell cmdlets
let powershellCommands =
    MiscEvents | where ActionType == "PowerShellCommand"
    | project PowershellCommand=extractjson("$.Command", AdditionalFields, typeof(string)), InitiatingProcessCommandLine, InitiatingProcessParentFileName, EventTime, MachineId
    | where PowershellCommand !endswith ".ps1" and PowershellCommand !endswith ".exe";
// Filter Powershell cmdlets executed on relevant machine and time period
powershellCommands | where MachineId == machineId and EventTime between ((alertTime-5min) .. 10min)
// Filter out common powershell cmdlets
| join kind=leftanti (powershellCommands | summarize MachineCount=dcount(MachineId) by PowershellCommand | where MachineCount > 20) on PowershellCommand
